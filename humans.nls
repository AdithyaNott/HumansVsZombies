; **************
; *** SETUP  ***
; **************

breed [humans human]
humans-own [ is-live speed cooldown-timer zone-zombies ]

; Initialization
to human-init
  set shape "person"
  set size 2
  set color blue
  setxy random-xcor random-ycor
  set is-live true
  set speed human-speed / ticks-per-second
  set cooldown-timer 0
end
  
; **************************
; *** ACTION PROCEDURES  ***
; **************************

to human-ai
  ; If there are no live zombies, stop.
  if not any? zombies with [ is-live ] [ stop ]
  human-move
  throw-sock
end

to human-move
  (ifelse 
    ; Move away from the nearest zombie  
    (human-move-style = "nearest")
    [ face find-nearest-zombie
      rt 180
      fd speed
    ]
    ; Move away from the center-of-mass of nearby zombies. If none are close, stand still.
    (human-move-style = "zone-evasion")
    [ set zone-zombies zombies in-radius zone-evasion-radius with [ is-live ]
      if any? zone-zombies 
      [ facexy (sum ([xcor] of zone-zombies) / count (zone-zombies)) (sum ([ycor] of zone-zombies) / count (zone-zombies))
        rt 180
        fd speed
      ]
    ]
  )
  
  
end

to throw-sock
  ; if there is a nearest zombie, they are within sock range, and cooldown is less than 0
  if find-nearest-zombie != nobody
  [ if (distance find-nearest-zombie <= sock-shot-range) and (cooldown-timer <= 0)
    ; throw a sock at them
    [ hatch-socks 1 
      [ sock-init
        face find-nearest-zombie ]
      set cooldown-timer sock-cooldown
    ]
  ]
  ; reduce cooldown
  set cooldown-timer (cooldown-timer - 1 / ticks-per-second)
end

; **************************
; *** UTILITY PROCEDURES ***
; **************************

to-report find-nearest-human
  report min-one-of humans with [ is-live ] [distance myself]
end
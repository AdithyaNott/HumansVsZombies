; **************
; *** SETUP  ***
; **************

breed [humans human]
humans-own [ is-live run-speed projectile-type projectile-range projectile-speed projectile-inaccuracy projectiles-remaining projectile-cooldown cooldown-timer jam-rate nearest-zombie zone-zombies ]

to human-init
  set shape "person"
  set size 6 ; 6 feet tall
  (ifelse
    (scenario = "Random distribution")
    [setxy random-xcor random-ycor]
    ; Set all humans to the bottom half of the field.
    (scenario = "Charge")
    [setxy (random-xcor) (random (min-pycor) / 2)]
  )
  set is-live true
  set run-speed (human-speed / ticks-per-second)
  set cooldown-timer 0
end

to sock-human-init
  set projectile-type "sock"
  set color blue + 1
  set projectile-range (sock-range)
  set projectile-speed (sock-speed / ticks-per-second)
  set projectile-inaccuracy (sock-inaccuracy)
  set projectiles-remaining (starting-socks)
  set projectile-cooldown (sock-cooldown)
  set jam-rate (sock-jam-rate) / 100
  ifelse show-number-projectiles
  [ set label projectiles-remaining ]
  [ set label "" ]
end

to blaster-human-init
  set projectile-type "blaster"
  set color blue
  set projectile-range (dart-range)
  set projectile-speed (dart-speed / ticks-per-second)
  set projectile-inaccuracy (dart-inaccuracy)
  set projectiles-remaining (starting-darts)
  set projectile-cooldown (dart-cooldown)
  set jam-rate (dart-jam-rate) / 100
  ifelse show-number-projectiles
  [ set label projectiles-remaining ]
  [ set label "" ]
end
  
; **************************
; *** ACTION PROCEDURES  ***
; **************************

to human-ai
  human-move
  launch-projectile
  update-human-label
end

to human-move
  ; Select between movement modes
  (ifelse 
    (human-move-style = "nearest")
    [ human-move-nearest ]
    (human-move-style = "zone-evasion")
    [ human-move-zone-evasion ]
  )
end

to human-move-nearest
  ; Move directly away from nearest zombie
  set nearest-zombie find-nearest-zombie
  if nearest-zombie != Nobody 
  [ face find-nearest-zombie
    rt 180
    fd run-speed ]
end

to human-move-zone-evasion
  ; Move away from the center-of-mass of nearby zombies.
  set zone-zombies zombies in-radius zone-evasion-radius with [ is-live ]
  if any? zone-zombies 
  [ facexy (sum ([xcor] of zone-zombies) / count (zone-zombies)) (sum ([ycor] of zone-zombies) / count (zone-zombies))
    rt 180
    fd run-speed ]
end

to launch-projectile
  set nearest-zombie (find-nearest-zombie)
  if find-nearest-zombie != nobody
  ; Launch projectile at nearest zombie within range if cooldown is less than 0.
  [ if (distance find-nearest-zombie <= projectile-range) and (cooldown-timer <= 0) and (projectiles-remaining > 0)
    ; Check for jam
    [ if ((random-float (1.00)) <= jam-rate)
      [ set projectiles-remaining (0)
        stop ]
      ; "Hatch projectile", inheriting all properties of Human
      hatch-projectiles 1
      [ projectile-init
        face (find-nearest-zombie) ; Human choses where to shoot. For now, shoot nearest zombie.
      ]
      set cooldown-timer (projectile-cooldown)
      set projectiles-remaining (projectiles-remaining - 1)
    ]
  ]
  set cooldown-timer (cooldown-timer - 1 / ticks-per-second)
end

to update-human-label
  ifelse show-number-projectiles
  [ set label projectiles-remaining ]
  [ set label "" ]
end

; **************************
; *** UTILITY PROCEDURES ***
; **************************

to-report find-nearest-human
  report min-one-of humans with [ is-live ] [distance myself]
end